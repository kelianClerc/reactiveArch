apply from: '../config.gradle'

def dependencyGroup(Closure closure) {
    closure.delegate = dependencies
    return closure
}

def jars = dependencyGroup {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

def kotlin = dependencyGroup {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$versions.kotlin"
}

def ui = dependencyGroup {
    implementation "com.android.support:appcompat-v7:$versions.androidSupport"
    implementation "com.android.support:design:$versions.androidSupport"
    implementation "com.android.support:recyclerview-v7:$versions.androidSupport"
    implementation "com.android.support.constraint:constraint-layout:1.1.0-beta3"
    implementation "com.squareup.picasso:picasso:2.5.2"
    implementation "com.github.gturedi:stateful-layout:1.2.1"
}

def architecture = dependencyGroup {
    def architecture = '1.0.0'
    implementation "android.arch.lifecycle:runtime:1.0.3"
    implementation "android.arch.lifecycle:extensions:$architecture"
    implementation "android.arch.lifecycle:reactivestreams:$architecture"
    kapt "android.arch.lifecycle:compiler:$architecture"
}

def network = dependencyGroup {
    def retrofit = '2.3.0'
    def okhttp = '3.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit"
    implementation "com.squareup.okhttp3:okhttp:$okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp"
}

def rx = dependencyGroup {
    def rxBinding = '2.0.0'
    implementation "io.reactivex.rxjava2:rxjava:2.1.3"
    implementation "io.reactivex.rxjava2:rxkotlin:2.1.0"
    implementation "io.reactivex.rxjava2:rxandroid:2.0.1"
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$rxBinding"
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4-kotlin:$rxBinding"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:$rxBinding"
    implementation "com.jakewharton.rxbinding2:rxbinding-design-kotlin:$rxBinding"
    implementation "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7-kotlin:$rxBinding"
}

def di = dependencyGroup {
    def dagger = '2.11'
    implementation "com.google.dagger:dagger:$dagger"
    implementation "com.google.dagger:dagger-android:$dagger"
    implementation "com.google.dagger:dagger-android-support:$dagger"
    kapt "com.google.dagger:dagger-android-processor:$dagger"
    kaptAndroidTest "com.google.dagger:dagger-android-processor:$dagger"
    kapt "com.google.dagger:dagger-compiler:$dagger"
    kaptTest "com.google.dagger:dagger-compiler:$dagger"
    kaptAndroidTest "com.google.dagger:dagger-compiler:$dagger"
}

def utils = dependencyGroup {
    def jodaAndroid = '2.9.9.1'
    debugImplementation"net.danlew:android.joda:$jodaAndroid"
    distribImplementation"net.danlew:android.joda:$jodaAndroid"
    releaseImplementation"net.danlew:android.joda:$jodaAndroid"
    implementation "com.android.support:support-annotations:$versions.androidSupport"
    implementation "net.hockeyapp.android:HockeySDK:5.0.0"
    implementation "com.android.support:multidex:1.0.2"
    implementation "com.github.aartikov:Alligator:1.0.0"
    implementation "com.karumi:dexter:4.1.0"
    implementation "com.jakewharton.timber:timber:4.5.1"
    implementation "androidx.core:core-ktx:0.1"
}

def debug = dependencyGroup {
    def chuck = '1.0.2'
    debugImplementation"com.readystatesoftware.chuck:library:$chuck"
    distribImplementation"com.readystatesoftware.chuck:library:$chuck"
    releaseImplementation"com.readystatesoftware.chuck:library-no-op:$chuck"
    debugImplementation"com.squareup.leakcanary:leakcanary-android:1.5.1"
}

def tests = dependencyGroup {
    def espresso = '3.0.1'
    testImplementation"junit:junit:4.12"
    testImplementation"org.mockito:mockito-core:2.10.0"
    testImplementation"de.bechte.junit:junit-hierarchicalcontextrunner:4.12.1"
    testImplementation"joda-time:joda-time:2.9.9"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espresso", {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$espresso", {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$espresso"
    androidTestImplementation "com.android.support.test:runner:1.0.1"
    androidTestImplementation "com.android.support.test.uiautomator:uiautomator-v18:2.1.3"
    androidTestImplementation "com.squareup.rx.idler:rx2-idler:0.9.0", {
        exclude group: 'io.reactivex.rxjava2', module: 'rxjava'
    }
}

dependencies {
    kotlin()
    architecture()
    ui()
    network()
    rx()
    di()
    utils()
    debug()
    tests()
}

